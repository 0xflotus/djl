plugins {
    id 'maven-publish'
}

dependencies {
    compile "org.slf4j:slf4j-api:${slf4j_version}"

    testCompile "org.testng:testng:${testng_version}"
    testCompile "org.slf4j:slf4j-simple:${slf4j_version}"
    testCompile "org.powermock:powermock-module-testng:${powermock_version}"
    testCompile "org.powermock:powermock-api-mockito2:${powermock_version}"
}

javadoc {
    title "Joule ${project.version} API specification"
    exclude "software/amazon/ai/util/**"
    exclude "software/amazon/ai/ndarray/internal/**"
    options.overview "src/main/javadoc/overview.html"
    options.addBooleanOption("-allow-script-in-comments",true)
    options.header "<script type='text/javascript' src='https://cdnjs.cloudflare.com/ajax/libs/mathjax/2.7.1/MathJax.js?config=TeX-AMS-MML_HTMLorMML'></script>"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId project.getGroup()
            artifactId project.getName()
            version rootProject.getVersion()
            from components.java
            artifacts = [jar, javadocJar, sourcesJar]
            pom {
                name = "Joule API"
                description = "A Deep Learning API in Java"
                url = "http://aws.com/Joule"
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://aws.amazon.com/apache2.0/'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "local"
            url = project.buildDir.absolutePath + '/repo'
        }
    }
}
