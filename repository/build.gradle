plugins {
    id 'maven-publish'
    id 'signing'
}

dependencies {
    compile "org.slf4j:slf4j-api:${slf4j_version}"
    compile "com.google.code.gson:gson:${gson_version}"
    compile project(":api")

    testCompile ("org.testng:testng:${testng_version}") {
        exclude group: "junit", module: "junit"
    }
    testCompile "org.slf4j:slf4j-simple:${slf4j_version}"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

if (shouldSign()) {
    signing {
        useGpgCmd()
        sign publishing.publications
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId project.getGroup()
            artifactId project.getName()
            version rootProject.getVersion()
            from components.java
            artifacts = [jar, javadocJar, sourcesJar]
            pom standardPom({
                name = "Repository API"
                description = "A respository specification for Deeplearning resources"
                url = url.get() + "repository"
            })
        }
    }
    repositories {
        maven publishRepository()
    }
}
