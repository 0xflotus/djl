group "ai.djl.sentencepiece"

dependencies {
    api project(":api")

    testImplementation("org.testng:testng:${testng_version}") {
        exclude group: "junit", module: "junit"
    }

    testImplementation "org.slf4j:slf4j-simple:${slf4j_version}"
}

processResources {
    doLast {
        def url = "https://djl-ai.s3.amazonaws.com/publish/sentencepiece-${sentencepiece_version}/jnilib"
        def files = [
                "linux-x86-64": "libsentencepiece_native.so",
                "osx-x86-64"  : "libsentencepiece_native.dylib"
        ]
        def jniDir = "${project.buildDir}/jnilib/native/lib"
        files.each { entry ->
            def downloadPath = new URL("${url}/${entry.key}/${entry.value}")
            def file = file("${jniDir}/${entry.key}/${entry.value}")
            if (!file.exists()) {
                project.logger.lifecycle("${downloadPath}")
                file.getParentFile().mkdirs()
                downloadPath.withInputStream { i -> file.withOutputStream { it << i } }
            }
        }
    }
}

// Create a properties files that contains version information
jar {
    def jniDir = "${project.buildDir}/jnilib"
    doFirst {
        def versionName = "${sentencepiece_version}"
        boolean isRelease = project.hasProperty("release") || project.hasProperty("staging")
        if (!isRelease) {
            version += String.format("-%s", new Date().format('yyyyMMdd'))
        }
        def dir = file("${jniDir}/native/lib")
        dir.mkdirs()
        def propFile = file("${jniDir}/native/lib/sentencepiece.properties")
        propFile.text = "version=${versionName}\n"
    }

    from jniDir
    from "jnilib"
}

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                name = "DJL NLP utilities for SentencePiece"
                description = "Deep Java Library (DJL) NLP utilities for SentencePiece"
                url = "http://www.djl.ai/sentencepiece/${project.name}"
            }
        }
    }
}

apply from: file("${rootProject.projectDir}/tools/gradle/cpp-formatter.gradle")

task compileJNI {
    doFirst {
        if (System.properties['os.name'].toLowerCase(Locale.ROOT).contains("mac")
                || System.properties['os.name'].toLowerCase(Locale.ROOT).contains("linux")) {
            exec {
                commandLine 'bash', 'build.sh'
            }
        } else {
            throw new IllegalStateException("Unknown Architecture " + System.properties['os.name'])
        }
    }
}
