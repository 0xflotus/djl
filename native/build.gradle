plugins {
    id 'maven-publish'
}

group = "org.apache.mxnet"
version = "1.5.0-SNAPSHOT"

def classifier = project.findProperty("classifier") ?: "osx-x86_64"
def flavor = project.findProperty("flavor") ?: "mkl"

task nativeJar(type: Jar) {
    doFirst {
        def propFile = file("src/main/resources/native/lib/mxnet.properties")
        propFile.delete()
        def dsStore = file("src/main/resources/native/lib/.DS_Store")
        dsStore.delete()

        def versionName = String.format("1.5.0-%s", new Date().format('yyyyMMdd'))
        def dir = file("src/main/resources/native/lib")
        def sb = new StringBuilder()
        sb.append("version=${versionName}\nlibraries=")
        def first = true
        for (String name : dir.list().sort()) {
            if (first) {
                first = false;
            } else {
                sb.append(',')
            }
            sb.append(name)
        }
        propFile.text = sb.toString()
    }
    from sourceSets.main.resources
    archiveClassifier = classifier
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId "mxnet-native-${flavor}"
            from components.java
            artifacts = [nativeJar]
            pom {
                name = "MXNet native library"
                description = "MXNet Engine native library binary distribution"
                url = "http://mxnet.apache.org"
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "local"
            url = project.buildDir.absolutePath + '/repo'
        }
    }
}
