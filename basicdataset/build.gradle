plugins {
    id 'maven-publish'
}

repositories {
    maven {
        url 'https://djl-ai.s3.amazonaws.com/dev'
    }
}

def classifier = getClassifier()

dependencies {
    compile "org.slf4j:slf4j-api:${slf4j_version}"
    compile project(":repository")

    testCompile ("org.testng:testng:${testng_version}") {
        exclude group: "junit", module: "junit"
    }
    testCompile "org.slf4j:slf4j-simple:${slf4j_version}"

    testCompile project(":mxnet:engine")
    testRuntime "ai.djl.mxnet:mxnet-native-mkl:1.6.0-SNAPSHOT:${classifier}"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

task syncS3(type:Exec) {
    commandLine "sh", "-c", "find . -name .DS_Store | xargs rm && aws s3 sync src/test/resources/mlrepo s3://djl-ai/mlrepo --acl public-read"

    standardOutput = new ByteArrayOutputStream()
    ext.output = {
        return standardOutput.toString()
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId project.getGroup()
            artifactId project.getName()
            version rootProject.getVersion()
            from components.java
            artifacts = [jar, javadocJar, sourcesJar]
            pom {
                name = "Deeplearning Java Library - Basic Dataset"
                description = "Basic Dataset for djl.ai"
                url = "http://www.djl.ai/mxnet/basicdataset"
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "local"
            url = project.parent.buildDir.absolutePath + '/repo'
        }
        maven {
            name = "s3"
            url = "s3://djl-ai/dev"
            authentication {
                awsIm(AwsImAuthentication)
            }
        }
    }
}
