plugins {
    id 'maven-publish'
}

dependencies {
    compile "net.java.dev.jna:jna:${jna_version}"
    compile "org.slf4j:slf4j-api:${slf4j_version}"
    compile project(":api")

    testCompile "org.testng:testng:${testng_version}"
    testCompile "org.slf4j:slf4j-simple:${slf4j_version}"
    testCompile "org.powermock:powermock-module-testng:${powermock_version}"
    testCompile "org.powermock:powermock-api-mockito2:${powermock_version}"
    testCompile project(':api').sourceSets.test.output
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'build/generated-src']
        }
    }
}

compileTestJava.dependsOn tasks.getByPath(':api:testClasses')

checkstyleMain.source = 'src/main/java'
pmdMain.source = 'src/main/java'

task jnarator(dependsOn: ":jnarator:jar") {
    doLast {
        File jnaGenerator = project(":jnarator").jar.outputs.files.singleFile
        javaexec {
            main = "-jar"
            args = [
                    jnaGenerator.absolutePath,
                    "-l",
                    "mxnet",
                    "-p",
                    "org.apache.mxnet.jna",
                    "-o",
                    "${project.buildDir}/generated-src",
                    "-m",
                    "${project.projectDir}/src/main/jna/mapping.properties",
                    "-f",
                    "src/main/include/mxnet/c_api.h",
                    "src/main/include/nnvm/c_api.h"
            ]
        }
    }
}

import java.util.regex.Matcher
import java.util.regex.Pattern

def checkForUpdate(String path, String url){
    def expected = new URL(url).text
    def actual = new File("${project.projectDir}/src/main/include/${path}").text
    if (!actual.equals(expected)) {
        def fileName = path.replaceAll("[/\\\\]", '_')
        (new File("${project.projectDir}/build/${fileName}")).text = expected
        logger.warn("[\033[31mWARN\033[0m ] Header file has been changed in open source project: ${path}.")
    }
}

task checkHeaderFile() {
    if (gradle.startParameter.offline) {
        logger.warn("[\033[31mWARN\033[0m ] Ignore header validation in offline mode.")
        return
    }

    def mxnetUrl = "https://raw.githubusercontent.com/apache/incubator-mxnet/master/"
    checkForUpdate("mxnet/c_api.h", "${mxnetUrl}/include/mxnet/c_api.h")
    def content = new URL("https://github.com/apache/incubator-mxnet/tree/master/3rdparty").text

    Pattern pattern = Pattern.compile("href=\"/dmlc/tvm/tree/([a-z0-9]+)\"")
    Matcher m = pattern.matcher(content);
    if (!m.find()) {
        throw new GradleException("Failed to retrieve submodule hash for tvm from github")
    }
    String hash = m.group(1);

    def nnvmUrl = "https://raw.githubusercontent.com/dmlc/tvm/${hash}"
    checkForUpdate("nnvm/c_api.h", "${nnvmUrl}/nnvm/include/nnvm/c_api.h")
}

compileJava.dependsOn(jnarator)
check.dependsOn checkHeaderFile

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId project.getGroup()
            artifactId project.getName()
            version rootProject.getVersion()
            from components.java
            artifacts = [jar, javadocJar, sourcesJar]
            pom {
                name = "MXNet Java API"
                description = "Java API for MXNet"
                url = "http://mxnet.apache.org"
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "local"
            url = project.buildDir.absolutePath + '/repo'
        }
    }
}
