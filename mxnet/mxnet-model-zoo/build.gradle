group 'ai.djl.mxnet'

repositories {
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    api project(":repository")
    api project(":mxnet:mxnet-engine")

    testRuntimeOnly "ai.djl.mxnet:mxnet-native-auto:${mxnet_version}-SNAPSHOT"
    testImplementation project(":basicdataset")
    testImplementation project(":model-zoo")
    testImplementation(project(":integration")) {
        exclude group: "org.apache.logging.log4j", module: "log4j-slf4j-impl"
        exclude group: "ai.djl.pytorch", module: "pytorch-engine"
        exclude group: "ai.djl.pytorch", module: "pytorch-model-zoo"
        exclude group: "ai.djl.tensorflow", module: "tensorflow-engine"
        exclude group: "ai.djl.tensorflow", module: "tensorflow-model-zoo"
    }

    testImplementation("org.testng:testng:${testng_version}") {
        exclude group: "junit", module: "junit"
    }
    testImplementation "org.slf4j:slf4j-simple:${slf4j_version}"
}

task syncS3(type: Exec) {
    commandLine "sh", "-c", "find . -name .DS_Store | xargs rm && aws s3 sync src/test/resources/mlrepo s3://djl-ai/mlrepo --acl public-read"

    standardOutput = new ByteArrayOutputStream()
    ext.output = {
        return standardOutput.toString()
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                name = "MXNet model zoo"
                description = "MXNet model zoo for DJL"
                url = "http://www.djl.ai/mxnet/${project.name}"
            }
        }
    }
}
