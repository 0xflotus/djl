plugins {
    id 'maven-publish'
}

group = "ai.djl.mxnet"
version = "1.6.0" + (project.hasProperty("release") ? "" : "-SNAPSHOT")

def flavor = project.findProperty("flavor") ?: "mkl"

def platformNames = file("src/main/resources").list() ?: []

def artifactsNames = []

platformNames.each { osName ->
    tasks.create(name: "${osName}Jar", type: Jar) {
        doFirst {
            def propFile = file("src/main/resources/${osName}/native/lib/mxnet.properties")
            propFile.delete()
            def dsStore = file("src/main/resources/${osName}/native/lib/.DS_Store")
            dsStore.delete()

            def versionName = String.format("1.5.0-%s", new Date().format('yyyyMMdd'))
            def dir = file("src/main/resources/${osName}/native/lib")
            def sb = new StringBuilder()
            sb.append("version=${versionName}\nlibraries=")
            def first = true
            for (String name : dir.list().sort()) {
                if (first) {
                    first = false;
                } else {
                    sb.append(',')
                }
                sb.append(name)
            }
            propFile.text = sb.toString()
        }
        from file("src/main/resources/${osName}")
        archiveClassifier = "${osName}-x86_64"
    }
    artifactsNames.add(tasks["${osName}Jar"])
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId "mxnet-native-${flavor}"
            from components.java
            artifacts = artifactsNames
            pom {
                name = "MXNet native library"
                description = "MXNet Engine native library binary distribution"
                url = "http://www.djl.ai/mxnet/native"
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "local"
            url = project.buildDir.absolutePath + '/repo'
        }
    }
}
