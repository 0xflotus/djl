plugins {
    id 'maven-publish'
    id 'signing'
}

dependencies {
    api project(":repository")

    testImplementation("org.testng:testng:${testng_version}") {
        exclude group: "junit", module: "junit"
    }
    testImplementation "org.slf4j:slf4j-simple:${slf4j_version}"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

if (shouldSign()) {
    signing {
        useGpgCmd()
        sign publishing.publications
    }
}

task syncS3(type: Exec) {
    commandLine "sh", "-c", "find . -name .DS_Store | xargs rm && aws s3 sync src/test/resources/mlrepo s3://djl-ai/mlrepo --acl public-read"

    standardOutput = new ByteArrayOutputStream()
    ext.output = {
        return standardOutput.toString()
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId project.getGroup()
            artifactId project.getName()
            version rootProject.getVersion()
            from components.java
            artifacts = [jar, javadocJar, sourcesJar]
            pom standardPom({
                name = "Model Zoo"
                delegate.description = "A built-in model zoo gallary for DJL"
                url = url.get() + "model-zoo"
            })
        }
    }
    repositories {
        maven publishRepository()
    }
}
