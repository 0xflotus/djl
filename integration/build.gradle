plugins {
    id 'application'
    id 'jacoco'
}

repositories {
    maven {
        url 'https://djl-ai.s3.amazonaws.com/repo'
    }
}

def classifier = getClassifier()

dependencies {
    compile "commons-cli:commons-cli:${commons_cli_version}"
    compile "org.slf4j:slf4j-simple:${slf4j_version}"
    compile project(":repository")
    compile project(":model-zoo")
    compile project(":mxnet:engine")
    compile project(":mxnet:mxnet-dataset")
    compile project(":mxnet:mxnet-model-zoo")
    compile ("org.testng:testng:${testng_version}") {
        exclude group: "junit", module: "junit"
    }

    runtime "ai.djl.mxnet:mxnet-native-mkl:1.5.0-SNAPSHOT:${classifier}"
}

task copyDependencies(type: Copy) {
    into "build/dependencies"
    from configurations.runtime
}

application {
    mainClassName = System.getProperty("main", "ai.djl.integration.tests.MxNDArrayOperatorsTest")
}

jacoco {
    toolVersion = "0.8.4"
}

jacocoTestReport {
    group = "Reporting"
    sourceDirectories = files()
    classDirectories = files()

    javaProjects().findAll { p ->
        ![":mxnet:jnarator", ":tensorflow:engine", ":examples"].contains(p.getPath())
    }.each { p ->
        additionalSourceDirs files((Set<File>) p.sourceSets.main.allJava.srcDirs)
        additionalClassDirs((FileCollection) p.sourceSets.main.output)
    }

    reports {
        xml.enabled true
        csv.enabled false
    }

    // Work-around to allow us to build list of executionData files in doFirst
    onlyIf = {
        true
    }
}


tasks.distTar.enabled = false
test.finalizedBy project.tasks.jacocoTestReport

check.dependsOn jacocoTestReport
check.dependsOn jacocoTestCoverageVerification

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.1
            }
        }
    }
}
