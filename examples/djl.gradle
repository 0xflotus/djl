/**
 * This build file is used by Deep Java Library (DJL) project internally.
 */

plugins {
    id 'application'
    id 'maven-publish'
    id 'signing'
}

repositories {
    maven {
        url 'https://djl-ai.s3.amazonaws.com/dev'
    }
}

def classifier = getClassifier()

dependencies {
    implementation "commons-cli:commons-cli:${commons_cli_version}"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:${log4j_slf4j_version}"
    implementation "com.google.code.gson:gson:${gson_version}"
    implementation project(":api")
    implementation project(":basicdataset")
    implementation project(":model-zoo")
    implementation project(":mxnet:mxnet-model-zoo")

    runtimeOnly "ai.djl.mxnet:mxnet-native-mkl:1.6.0-SNAPSHOT:${classifier}"

    testImplementation("org.testng:testng:${testng_version}") {
        exclude group: "junit", module: "junit"
    }
}


task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId project.getGroup()
            artifactId project.getName()
            version rootProject.getVersion()
            from components.java
            artifacts = [jar, sourcesJar, javadocJar]
            pom standardPom({
                name = "Examples"
                delegate.description = "Examples for DJL"
                url = url.get() + "examples"
            })
            pom.withXml {
                Node pomNode = asNode()
                pomNode.dependencies.'*'.findAll() {
                    it.artifactId.text() == 'mxnet-native-mkl'
                }.each() {
                    it.parent().remove(it)
                }
            }
        }
    }
    repositories {
        maven publishRepository()
    }
}

application {
    mainClassName = System.getProperty("main", "ai.djl.examples.inference.ObjectDetection")
}

run {
    systemProperties System.getProperties()
    systemProperties.remove("user.dir");
}

tasks.distTar.enabled = false
