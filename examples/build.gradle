plugins {
    id "java-library"
    id 'application'
}

repositories {
    jcenter()
    maven {
        url 'https://djl-ai.s3.amazonaws.com/dev'
    }
}

def classifier = getClassifier()

dependencies {
    api "commons-cli:commons-cli:1.4"
    api "org.apache.logging.log4j:log4j-slf4j-impl:2.12.1"
    api "com.google.code.gson:gson:2.8.5"
    api "ai.djl:api:0.2.0-SNAPSHOT"
    api "ai.djl:basicdataset:0.2.0-SNAPSHOT"
    api "ai.djl:model-zoo:0.2.0-SNAPSHOT"
    api "ai.djl.mxnet:mxnet-model-zoo:0.2.0-SNAPSHOT"
    runtimeOnly "ai.djl.mxnet:mxnet-native-mkl:1.6.0-SNAPSHOT:${classifier}"

    testImplementation 'org.testng:testng:6.14.3'
}

test {
    maxHeapSize = "1024m"

    useTestNG()
}

application {
    mainClassName = System.getProperty("main", "ai.djl.examples.inference.ObjectDetection")
}

run {
    systemProperties System.getProperties()
}

tasks.distTar.enabled = false

def getClassifier() {
    String osName = System.getProperty("os.name")
    String osPrefix;
    if (osName.startsWith("Win")) {
        osPrefix = "win"
    } else if (osName.startsWith("Mac")) {
        osPrefix = "osx"
    } else if (osName.startsWith("Linux")) {
        osPrefix = "linux"
    } else {
        throw new GradleException("Unsupported os: " + osName)
    }
    return osPrefix + "-x86_64"
}
