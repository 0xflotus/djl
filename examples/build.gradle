plugins {
    id "java"
    id 'application'
}

repositories {
    jcenter()
    maven {
        url 'https://djl-ai.s3.amazonaws.com/dev'
    }
}

def classifier = getClassifier()

dependencies {
    compile "commons-cli:commons-cli:1.4"
    compile "org.apache.logging.log4j:log4j-slf4j-impl:2.12.1"
    compile "com.google.code.gson:gson:2.8.5"
    compile "ai.djl:api:0.1.0-SNAPSHOT"
    compile "ai.djl:basicdataset:0.1.0-SNAPSHOT"
    compile "ai.djl.mxnet:mxnet-model-zoo:0.1.0-SNAPSHOT"
    runtime "ai.djl.mxnet:mxnet-engine:0.1.0-SNAPSHOT"
    runtime "ai.djl.mxnet:mxnet-native-mkl:1.6.0-SNAPSHOT:${classifier}"

    testImplementation 'org.testng:testng:6.14.3'
}

test {
    maxHeapSize = "1024m"

    useTestNG()
}

application {
    mainClassName = System.getProperty("main", "ai.djl.examples.inference.ClassifyExample")
}

tasks.distTar.enabled = false

def getClassifier() {
    String osName = System.getProperty("os.name")
    String osPrefix;
    if (osName.startsWith("Win")) {
        osPrefix = "win"
    } else if (osName.startsWith("Mac")) {
        osPrefix = "osx"
    } else if (osName.startsWith("Linux")) {
        osPrefix = "linux"
    } else {
        throw new GradleException("Unsupported os: " + osName)
    }
    return osPrefix + "-x86_64"
}
